%include thesis.fmt

% \renewcommand{\indexname}{Function Index}
\chapter{Function Index}

This chapter provides an index to all the Haskell functions used in the thesis. Some functions are defined in the body of the thesis, while others are taken from the Haskell report \cite{haskell}. Many of the library functions are reproduced in \S\ref{secI:library}.

\renewenvironment{theindex}
    {\thispagestyle{plain}\parindent\z@@
     \parskip\z@@ \@@plus .3\p@@\relax
     \columnseprule \z@@
     \columnsep 35\p@@
     \let\item\@@idxitem}
    {}

\begin{multicols}{2}
\printindex
\end{multicols}


\section{Library Functions}
\label{secI:library}

The following functions are available in the Haskell standard libraries. All the functions are given a type signature, and most contain a possible implementation.

\begin{comment}
\begin{code}
import Prelude(Char,Show,Eq(..),Int,Num(..),Ord(..),otherwise,repeat,Read,Bool(..))
data IO a = IO a
instance Monad IO
\end{code}
\end{comment}


%format Bool2 = Bool
%format False2 = False
%format True2 = True

\ind{Prelude}\ind{String}\ind{flip}\ind{id}\ind{error}\ind{undefined}
\ind{True}\ind{False}\ind{Bool}\ind{fst}\ind{snd}\ind{const}\ind{not}
\ind{read}\ind{reads}\ind{show}
\begin{code}
module Prelude where

type String = [Char]

data Bool2 = False2 | True2

not :: Bool -> Bool
not x = if x then False else True

(&&),(||) :: Bool -> Bool -> Bool
a && b = if a then b else True
a || b = if a then True else b

(.) :: (beta -> gamma) -> (alpha -> beta) -> alpha -> gamma
(.) f g x = f (g x)

flip :: (alpha -> beta -> gamma) -> beta -> alpha -> gamma
flip f a b = f b a

id :: alpha -> alpha
id x = x

-- terminate with an error
error :: String -> alpha
\end{code}\begin{code}
undefined :: alpha
undefined = error "undefined"

fst :: (alpha,beta) -> alpha
fst (x,y) = x

snd :: (alpha,beta) -> beta
snd (x,y) = y

const :: alpha -> beta -> alpha
const x _ =  x

-- show an item as a string
show :: Show alpha=> alpha -> String

-- read an item from a string
read :: Read alpha => String -> alpha

-- read an item, returning all possible parses
reads :: Read alpha => String -> [(alpha, String)]
\end{code}

%format (List a) = [a]
%format Nil = []
%format Cons = (:)

\ind{List}\ind{map}\ind{any}\ind{all}\ind{zipWith}\ind{elem}\ind{length}\ind{iterate}\ind{zip}
\ind{notElem}\ind{splitAt}\ind{take}\ind{drop}\ind{break}\ind{span}\ind{takeWhile}\ind{dropWhile}
\ind{lookup}\ind{null}\ind{tail}\ind{head}\ind{replicate}\ind{filter}\ind{concat}\ind{nub}\ind{foldr}
\ind{and}\ind{or}\ind{concatMap}\ind{reverse}\ind{partition}
\begin{code}
module Data.List where

data List alpha = Nil | Cons alpha (List alpha)

length :: [alpha] -> Int
length []      = 0
length (x:xs)  = 1 + length xs

map :: (alpha -> beta) -> [alpha] -> [beta]
map f xs = [f x | x <- xs]

foldr :: (alpha-> beta -> beta) -> beta -> [alpha] -> beta
foldr f z []       = z
foldr f z (x:xs)   = f x (foldr f z xs)

(++) :: [alpha] -> [alpha] -> [alpha]
xs ++ ys = foldr (:) ys xs

or,and :: [Bool] -> Bool
or   = foldr (||)  False
and  = foldr (&&)  True

any, all :: (alpha -> Bool) -> [alpha] -> Bool
any  f = or    . map f
all  f = and   . map f

null :: [alpha] -> Bool
null []     = True
null (_:_)  = False

head :: [alpha] -> alpha
head (x:_) = x

tail :: [alpha] -> [alpha]
tail (_:xs) = xs

elem,notElem :: Eq alpha => alpha -> [alpha] -> Bool
elem     x = any  (==  x)
notElem  x = all  (/=  x)

zipWith :: (alpha -> beta -> gamma) -> [alpha] -> [beta] -> [gamma]
zipWith f (x:xs) (y:ys) = f x y : zipWith f xs ys
zipWith f _ _ = []

zip :: [alpha] -> [beta] -> [(alpha,beta)]
zip = zipWith (,)

lookup :: Eq alpha => alpha -> [(alpha,beta)] -> Maybe beta
lookup key []           =  Nothing
lookup key ((x,y):xys)  | key == x   = Just y
                        | otherwise  = lookup key xys

iterate :: (alpha -> alpha) -> alpha -> [alpha]
iterate f x = x : iterate f (f x)

splitAt :: Int -> [alpha] -> ([alpha],[alpha])
splitAt n xs = (take n xs, drop n xs)

take :: Int -> [alpha] -> [alpha]
take n _  | n <= 0  = []
take _ []           = []
take n (x:xs)       = x : take (n-1) xs

drop :: Int -> [alpha] -> [alpha]
drop n xs | n <= 0  =  xs
drop _ []           =  []
drop n (x:xs)       =  drop (n-1) xs

span, break :: (alpha -> Bool) -> [alpha] -> ([alpha],[alpha])
span p xs = (takeWhile p xs, dropWhile p xs)
break p =  span (not . p)

dropWhile :: (alpha -> Bool) -> [alpha] -> [alpha]
dropWhile _ []           = []
dropWhile p (x:xs)
            | p x        = dropWhile p xs
            | otherwise  = x:xs
\end{code}\begin{code}
takeWhile :: (alpha -> Bool) -> [alpha] -> [alpha]
takeWhile _ []           =  []
takeWhile p (x:xs)
            | p x        =  x : takeWhile p xs
            | otherwise  =  []

replicate :: Int -> alpha -> [alpha]
replicate n x =  take n (repeat x)

filter :: (alpha -> Bool) -> [alpha] -> [alpha]
filter p xs = [x | x <- xs, p x]

concat :: [[alpha]] -> [alpha]
concat = foldr (++) []

concatMap :: (alpha -> [beta]) -> [alpha] -> [beta]
concatMap f =  concat . map f

nub :: Eq alpha => [alpha] -> [alpha]
nub []      =  []
nub (x:xs)  =  x : nub (filter (/= x) xs)

reverse :: [alpha] -> [alpha]
reverse l =  rev l []
  where  rev []     a = a
         rev (x:xs) a = rev xs (x:a)

partition :: (alpha -> Bool) -> [alpha] -> ([alpha], [alpha])
partition p xs = (filter p xs, filter (not . p) xs)
\end{code}

\ind{isSpace}\ind{Char}
\begin{code}
module Data.Char where

-- is a character a space
isSpace :: Char -> Bool
\end{code}


\ind{Maybe}\ind{Nothing}\ind{Just}\ind{maybe}\ind{fromJust}\ind{fromMaybe}\ind{isNothing}
\begin{code}
module Data.Maybe where

data Maybe alpha = Nothing | Just alpha

maybe :: beta -> (alpha -> beta) -> Maybe alpha -> beta
maybe nothing just Nothing   = nothing
maybe nothing just (Just x)  = just x

fromMaybe :: alpha -> Maybe alpha -> alpha
fromMaybe x = maybe x id

fromJust :: Maybe alpha -> alpha
fromJust (Just x) = x

isNothing :: Maybe alpha -> Bool
isNothing Nothing  = True
isNothing _        = False
\end{code}

\ind{Monad}\ind{return}\ind{liftM}\ind{mapM}\ind{mapM\_}\ind{sequence}\ind{sequence\_}
\ind{Functor}\ind{fmap}
\begin{code}
module Control.Monad where

class Monad m where
		(>>=) :: m alpha -> (alpha -> m beta) -> m beta
		(>>) :: m alpha -> m beta -> m beta
		return :: alpha -> m alpha

class Functor f where
	  fmap :: (a -> b) -> f a -> f b

(=<<) :: Monad m => (alpha -> m beta) -> m alpha -> m beta
(=<<) = flip (>>=)

liftM :: Monad m => (alpha -> beta) -> m alpha -> m beta
liftM f x = x >>= (return . f)

mapM :: Monad m => (alpha -> m beta) -> [alpha] -> m [beta]
mapM f = sequence . map f

mapM_ :: Monad m => (alpha -> m beta) -> [alpha] -> m ()
mapM_ f = sequence_ . map f

sequence :: Monad m => [m alpha] -> m [alpha]
sequence ms = foldr k (return []) ms
	where k m ms = m >>= \x -> ms >>= \xs -> return (x:xs)

sequence_ :: Monad m => [m alpha] -> m ()
sequence_ ms = foldr (>>) (return ()) ms
\end{code}

\ind{State}\ind{runState}\ind{get}\ind{put}\ind{evalState}
\begin{code}
module Control.Monad.State where

newtype State s alpha = State {runState :: s -> (alpha, s)}
instance Monad (State s)

evalState :: State s alpha -> s -> alpha
evalState s = fst . runState s

get :: State s s
put :: s -> State s ()
\end{code}

\ind{Function}\ind{on}
\begin{code}
module Data.Function where

on :: (beta -> beta -> gamma) -> (alpha -> beta) -> alpha -> alpha -> gamma
on g f x y = g (f x) (f y)
\end{code}

\ind{System}\ind{IO}\ind{putChar}\ind{putStr}\ind{putStrLn}\ind{print}\ind{getArgs}\ind{getContents}
\begin{code}
module System.IO where

-- write out a character to the output
putChar :: Char -> IO ()

putStr :: String -> IO ()
putStr = mapM_ putChar

putStrLn :: String -> IO ()
putStrLn x = putStr x >> putChar '\n'

print :: Show alpha => alpha -> IO ()
print = putStrLn . show

-- get the command line arguments
getArgs :: IO [String]

-- get the input stream
getContents :: IO String
\end{code}

